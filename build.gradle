buildscript {
    ext {
        gradleVaadinPluginVersion = '0.10.2'
        springBootVersion = '1.2.7.RELEASE'
        springLoadedVersion = '1.2.4.RELEASE'
    }
    repositories.jcenter()
    dependencies {
        classpath "fi.jasoft.plugin:gradle-vaadin-plugin:$gradleVaadinPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.springframework:springloaded:$springLoadedVersion"
    }
}

ext {
    ext {
        groovyVersion = '2.4.5'
        gradleWrapperVersion = '2.8'
        vaadinVersion = '7.6.0.beta1'
        vaadinSpringVersion = '1.0.0'
    }
}

apply plugin: 'idea'
apply from: 'gradle/idea.gradle'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'fi.jasoft.plugin.vaadin'
apply plugin: 'spring-boot'
apply from: 'gradle/springboot-vaadin-fixes.gradle'

repositories {
    jcenter()
    maven { url 'https://maven.vaadin.com/vaadin-addons' }
    maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileGroovy {
	groovyOptions.configurationScript = file("gradle/groovyOptions.groovy")
}

mainClassName = "vdecl.Application"

jar {
    baseName = 'vdecl'
    // version = '0.1.0'
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

vaadin {
	vaadin.version = vaadinVersion
	vaadin.push = true
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile("com.vaadin:vaadin-spring-boot-starter:${vaadinSpringVersion}")
    compile 'commons-io:commons-io:2.4'
    compile 'net.engio:mbassador:1.2.4'
}

// Exclude all deps, that pull in newer versions of the critical libraries for
// widgetset compilation here (e.g. validation-api).  If there are errors
// building the widgetset, check with `./gradlew dependencies` first.
configurations {
	'vaadin-client' {
		exclude module: 'spring-boot-starter-web'
		exclude module: 'spring-boot-vaadin'
	}
}
