plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'com.devsoap.plugin.vaadin' version '1.4.1'
    id 'groovy'
    id 'com.palantir.git-version' version '0.12.0-rc2'
}

apply plugin: 'io.spring.dependency-management'

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://maven.vaadin.com/vaadin-addons' }
}

version gitVersion()

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileGroovy {
	groovyOptions.configurationScript = file("gradle/groovyOptions.groovy")
}

bootJar {
    version = null // no version in the filename
}

bootRun.systemProperties = System.properties // pass sysprops down to bootRun, so we can add them in configs in intellij

vaadin {
	vaadin.version = vaadinVersion
	push = true
	vaadinCompile.widgetset = 'vdecl.ui.widgetset'
	vaadinCompile.outputDirectory = file('build/resources/main/')
	vaadinThemeCompile.themesDirectory = file('build/resources/main/VAADIN/themes')
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:${groovyVersion}"

    implementation "org.clojure:clojure:${clojureVersion}"
    implementation "org.clojure:data.xml:0.0.8"
    implementation "org.clojure:test.check:0.9.0"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation("com.vaadin:vaadin-spring-boot-starter:${vaadinSpringVersion}")
    implementation 'commons-io:commons-io:2.6'
    implementation 'net.engio:mbassador:1.3.2'
}
